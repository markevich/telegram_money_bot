defmodule MarkevichMoney.Pipelines.Reports.MonthlyReport do
  use MarkevichMoney.Constants
  alias MarkevichMoney.CallbackData
  alias MarkevichMoney.Pipelines.Reports.MonthlyReport.{ComparedExpenses, ReactionRenderer}
  alias MarkevichMoney.Pipelines.Stats.Callbacks, as: GenericStats
  alias MarkevichMoney.Sleeper
  alias MarkevichMoney.Steps.Telegram.{SendMessage, SendSticker}
  alias MarkevichMoney.Transactions
  alias MarkevichMoney.Users

  def call(user_id) do
    user = Users.get_user!(user_id)

    %{
      current_user: user,
      chat_id: user.telegram_chat_id
    }
    |> send_message_about_stats_calculation()
    |> send_message_with_stats()
  end

  def send_message_about_stats_calculation(%{chat_id: chat_id} = payload) do
    sticker1 = Application.get_env(:markevich_money, :tg_file_ids)[:stickers][:"ğŸ‘´ğŸ˜"]

    SendSticker.call(%{
      chat_id: chat_id,
      file_id: sticker1,
      disable_notification: true
    })

    Sleeper.sleep()

    message1 =
      "ĞŸÑ€Ğ¸ÑˆĞ»Ğ¾ Ğ²Ñ€ĞµĞ¼Ñ Ğ¿Ğ¾ÑĞ¼Ğ¾Ñ‚Ñ€ĞµÑ‚ÑŒ, ĞºĞ°Ğº Ñ‚Ñ‹ Ñ‚Ñ€Ğ°Ñ‚Ğ¸Ğ» Ğ·Ğ¾Ğ»Ğ¾Ñ‚Ñ‹Ğµ Ğ² ÑÑ‚Ğ¾Ğ¼ Ğ¼ĞµÑÑÑ†Ğµ! ĞÑƒ-ĞºĞ°, ÑˆĞ¾ Ñ‚ÑƒÑ‚ Ñƒ Ğ½Ğ°Ñ..."

    SendMessage.call(%{chat_id: chat_id, output_message: message1})

    Sleeper.sleep()

    sticker2 = Application.get_env(:markevich_money, :tg_file_ids)[:stickers][:"ğŸ‘´ğŸ§®"]

    SendSticker.call(%{
      chat_id: chat_id,
      file_id: sticker2,
      disable_notification: true
    })

    Sleeper.sleep()

    message2 = "ĞĞ±Ğ±Ğ°Ğ¶Ğ¸ ĞµÑ‰Ñ‘ Ñ‡ÑƒÑ‚Ğ¾Ğº..."

    SendMessage.call(%{chat_id: chat_id, output_message: message2},
      disable_notification: true
    )

    Sleeper.sleep()

    payload
  end

  def send_message_with_stats(%{current_user: user}) do
    previous_month = Timex.shift(Timex.now(), months: -1)
    two_month_ago = Timex.shift(Timex.now(), months: -2)
    three_month_ago = Timex.shift(Timex.now(), months: -3)

    monthly_state = transactions_state(user, previous_month, two_month_ago, three_month_ago)

    case monthly_state do
      %{prev_month_exists: true, two_month_ago_exists: true, three_month_ago_exists: true} ->
        {:full_report_sent, send_full_compared_expenses(user, previous_month, two_month_ago)}

      %{prev_month_exists: true} ->
        {:short_report_sent, send_generic_monthly_expenses(user)}

      _ ->
        {:no_transactions, send_empty_report_reaction(user)}
    end
  end

  def transactions_state(user, prev_month, two_month_ago, three_month_ago) do
    %{
      prev_month_exists: Transactions.exists_in_month?(user.id, prev_month),
      two_month_ago_exists: Transactions.exists_in_month?(user.id, two_month_ago),
      three_month_ago_exists: Transactions.exists_in_month?(user.id, three_month_ago)
    }
  end

  def send_full_compared_expenses(user, previous_month, two_month_ago) do
    stats_previous_month =
      Transactions.stats(
        user,
        Timex.beginning_of_month(previous_month),
        Timex.end_of_month(previous_month)
      )

    stats_two_month_ago =
      Transactions.stats(
        user,
        Timex.beginning_of_month(two_month_ago),
        Timex.end_of_month(two_month_ago)
      )

    payload =
      ComparedExpenses.call(
        stats_two_month_ago,
        Timex.format!(two_month_ago, "{0M}.{YYYY}"),
        stats_previous_month,
        Timex.format!(previous_month, "{0M}.{YYYY}")
      )

    payload
    |> Map.put(:current_user, user)
    |> SendMessage.call()

    Sleeper.sleep()

    ReactionRenderer.render_full_report_reaction(
      percentage_diff: payload.percentage_diff,
      numeric_diff: payload.numeric_diff
    )
    |> send_reaction_messages(user)

    payload
  end

  def send_generic_monthly_expenses(user) do
    callback_data = %CallbackData{
      callback_data: %{"type" => @stats_callback_previous_month},
      current_user: user
    }

    callback_data
    # TODO: Make it a non callback function. We have nothing to respond to telegram in that case.
    |> GenericStats.call()

    Sleeper.sleep()

    ReactionRenderer.render_short_report_reaction()
    |> send_reaction_messages(user)
  end

  defp send_empty_report_reaction(user) do
    ReactionRenderer.render_empty_report_reaction()
    |> send_reaction_messages(user)
  end

  defp send_reaction_messages({:ok, message, sticker}, user) do
    %{
      chat_id: user.telegram_chat_id,
      file_id: sticker,
      disable_notification: true
    }
    |> SendSticker.call()

    Sleeper.sleep()

    %{
      chat_id: user.telegram_chat_id,
      output_message: message
    }
    |> SendMessage.call()
  end
end
